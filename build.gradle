buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

group "ru.jihor.rat"
version "0.0.1-SNAPSHOT"

apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.8
targetCompatibility = 1.8


repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'com.google.auto.service:auto-service:1.0-rc4'
    testCompile 'org.codehaus.groovy:groovy-all:2.4.15'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

// this will sadly NOT work in IntelliJ Idea, see https://youtrack.jetbrains.com/issue/IDEA-154038
gradle.projectsEvaluated {
    tasks.withType(GroovyCompile) {
        options.compilerArgs += ["-Arat.project.version=2.8-SNAPSHOT"]
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs += ["-Arat.project.version=2.8-SNAPSHOT"]
    }
}

if (project.name != 'demo') {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
    }

    publishing {
        publications {
            toMaven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }
            }
        }
    }

    bintray {
        user = project.hasProperty("user") ? project.property("user") : "fake"
        key = project.hasProperty("key") ? project.property("key") : "fake"

        publications = ['toMaven']
        publish = true

        pkg {
            repo = 'maven'
            name = rootProject.name
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/jihor/rat.git'
        }
    }

}